buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6"
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.1"
    }
}

apply plugin: "com.gradle.plugin-publish"
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven-publish'

group 'com.athaydes.pony.gradle'
version '0.1.0'
description 'A Gradle Plugin for the Pony programming language'

repositories {
    jcenter()
}

sourceCompatibility = 1.8

dependencies {
    compile gradleApi()
    compile localGroovy()

    testCompile "org.spockframework:spock-core:1.0-groovy-2.4", {
        transitive = false
    }
}


task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    classifier "sources"
    extension "jar"
}

task javadocJar( type: Jar ) {
    classifier "javadoc"
    from javadoc
}

/* Publishing config */

artifacts {
    archives javadocJar, sourcesJar
}

// add all the info required by Maven Central to the pom
configure( install.repositories.mavenInstaller ) {
    //repository(url: "file://$buildDir")
    pom.project {
        inceptionYear '2016'
        name project.name
        packaging 'jar'
        description project.description

        url 'https://github.com/renatoathaydes/pony-gradle-plugin'

        scm {
            connection 'git@github.com:renatoathaydes/pony-gradle-plugin.git'
            developerConnection 'git@github.com:renatoathaydes/pony-gradle-plugin.git'
            url 'https://github.com/renatoathaydes/pony-gradle-plugin'
        }

        licenses {
            license {
                name 'The Apache License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }

        developers {
            developer {
                id 'renatoathaydes'
                name 'Renato Athaydes'
                email 'renato@athaydes.com'
            }
        }
    }
}

pluginBundle {
    website = 'https://github.com/renatoathaydes/pony-gradle-plugin'
    vcsUrl = 'https://github.com/renatoathaydes/pony-gradle-plugin'

    //noinspection GroovyAssignabilityCheck
    plugins {
        ponyPlugin {
            id = 'com.athaydes.pony'
            displayName = 'Pony Gradle Plugin'
            description = project.description
            version = project.version
            tags = [ 'language', 'pony', 'gradle' ]
        }
    }
}

apply plugin: 'com.jfrog.bintray'

bintray {
    user = project.hasProperty("bintrayUserName") ? bintrayUserName : null
    key = project.hasProperty("bintrayApiKey") ? bintrayApiKey : null
    configurations = [ 'archives' ]
    publish = true
    pkg {
        repo = 'maven'
        name = 'pony-gradle-plugin'
        licenses = ['Apache-2.0']
        desc = project.description
        vcsUrl = 'https://github.com/renatoathaydes/pony-gradle-plugin.git'
        websiteUrl = 'https://github.com/renatoathaydes/pony-gradle-plugin'
        issueTrackerUrl = 'https://github.com/renatoathaydes/pony-gradle-plugin/issues'
        labels = [ 'language', 'pony', 'gradle' ]
        publicDownloadNumbers = true

        //noinspection GroovyAssignabilityCheck
        version {
            name = project.version
            vcsTag = project.version
            gpg {
                sign = true
            }
            mavenCentralSync {
                sync = true
                user = ossrhUsername
                password = ossrhPassword
                close = '1' // '0' to NOT close
            }
        }
    }

}

bintrayUpload.dependsOn build, sourcesJar
publishPlugins.dependsOn build, sourcesJar